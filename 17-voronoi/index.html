<!DOCTYPE HTML>
<html>
<head>
<link href = 'https://fonts.googleapis.com/css?family=Open+Sans:400,700' rel = 'stylesheet' type = 'text/css'>
<style>
html, body {
	margin: 0; padding: 0;
	background-color: #112;
	color: #85f;

	font-family: 'open sans', sans-serif;
	font-size: 14px;
}
h2 {
	color: #8df;
	font-size: 18px;
}

/*UI*/

#container {
	width: 720px;
	margin: 4px auto;
}
#credits{
	margin-top: 16px;
	font-size: 12px;
}

/* WORLD*/

#world {
	border: 1px #ccc none;/*dashed;*/

	margin: 4px auto;

	position: relative;

	width: 720px;
	height: 720px;

}
<title>Voronoi Relaxation</title>
</style>

<script id = "v_quad" type = "text/glsl">#version 300 es
in vec2 x; void main () { gl_Position = vec4(x, 0., 1.); }
</script>

<script id = "v_seed" type = "text/glsl">#version 300 es
uniform sampler2D site;
in float i;
out float j;
void main ()
{
	gl_Position = vec4(texelFetch(site, ivec2(i, 0), 0).xy*2.-1., 0., 1.);
	j = i;
	gl_PointSize = 4.0;
}
</script>

<script id = "f_seed" type = "text/glsl">#version 300 es
precision highp float;

in float j;
uniform sampler2D site;

out vec4 frag_colour;

void main ()
{
	vec2 xy = texelFetch(site, ivec2(j, 0), 0).xy;
	frag_colour = vec4(xy, j+1., 1.);
}
</script>

<script id = "f_step" type = "text/glsl">#version 300 es
precision highp float;

uniform sampler2D prev;
uniform float stepsize;

out vec4 frag_colour;

const vec4 bg = vec4(0., 0., 0., 1.);
const vec2[8] dx = vec2[8](
	vec2( 1., 0.), vec2( 1., 1.), vec2( 0., 1.), vec2(-1., 1.),
	vec2(-1., 0.), vec2(-1.,-1.), vec2( 0.,-1.), vec2( 1.,-1.)
);

vec4 compare (vec2 xy, float len, vec4 col, vec2 offset) {
	vec4 st = texture(prev, xy + offset);
	return mix(col, st, float(
	(col == bg) || ((st != bg) && (distance(st.xy, xy) < len))));
}

void main ()
{
	vec2 xy = gl_FragCoord.xy / 720.;
	float l = stepsize/720.;
	float len;
	frag_colour = texture(prev, xy);
	for (int i = 0; i < 8; ++i) {
		frag_colour = compare(
			xy, distance(xy, frag_colour.xy),
			frag_colour, l*dx[i]);
	}
}
</script>

<script id = "f_1", type = "text/glsl">#version 300 es
#define rho 0.001
precision highp float;

uniform sampler2D src;
uniform vec2 cursor;
uniform int remove;
uniform float time;

out vec4 frag_colour;

vec2 drunk (vec4 c) {
	
	vec2 vel = 0.1*vec2(cos(3.1416*c.x), 0.); /* neat horizontal gradient */
	return c.xy + vel;
}

void main () {
	int Y = int(gl_FragCoord.y);
	int s = int(gl_FragCoord.x);

	frag_colour = vec4(0., 0., 0., 1.);
	float y = gl_FragCoord.y/720.;
	int j;
	int jx = int(texture(src, cursor).z-1.);
	vec4 c;
	for (int i = 0; i < 720; ++i) {
		c = texelFetch(src, ivec2(i, Y), 0);
		j = int(c.z-1.);
		frag_colour += mix(
			vec4(0., 0., 0., 0.),
			mix(
				vec4(vec2(float(i)/720., y)*rho, rho, 0.),
				vec4(drunk(c)*rho, rho, 0.),
				float(time > 0.)
			),
			float(j == s));
	}
	frag_colour = mix(
	frag_colour, vec4(1., 1., -1., 1.),
	float((s == jx) && bool(remove)));
}
</script>
<script id = "f_2", type = "text/glsl">#version 300 es
precision highp float;

uniform sampler2D src;
uniform int n;
uniform vec2 cursor;

out vec4 frag_colour;

void main () {
	ivec2 uv = ivec2(gl_FragCoord.xy);

	int i = uv.x;
	if (texelFetch(src, ivec2(i, 0), 0).z < 0.) {
		i = n-1;
	}

	frag_colour = vec4(0., 0., 0., 1.);
	for (int j = 0; j < 720; ++j) {
		frag_colour += texelFetch(src, ivec2(i, j), 0);
	}

	float b = float(i == n);

	vec2 Wx = mix(frag_colour.xy, cursor, b);
	float W = mix(frag_colour.z, 1., b);
	frag_colour = vec4(Wx/W, 0., 1.);
}
</script>
</head>
<body>

<div id = "container">
	<h2>VORONOI RELAXATION</h2>

	<canvas id = "world" width = "720" height = "720" oncontextmenu="return false;"></canvas>
	<script src="../shared/gl2.js"></script>	
	<script src="voronoi.js"></script>
	<div><p>Lloyd's algorithm(S. Lloyd, 1982) applied to approximate Voronoi diagram generated with jump-flooding as described by Rong &amp; Tan, 2006. Each generating site is repeatedly relaxed towards the centroid of its Voronoi cell. Centroid calculation done in a fragment shader; the page may be slow or render incorrectly on some hardware.</p>

	<ul>
	<li>RMB/LMB: add/remove cell.</li>
	</ul></div>

	<div id = "credits">
	J. Lo, June 2018.
	</div>

</div>

</body>
</html>
